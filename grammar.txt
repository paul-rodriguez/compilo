<PROGRAM> |=
			<PROGRAM_F> <PROGRAM_V>

<PROGRAM_V> |=
			<PROGRAM_F> <PROGRAM_V>
			| eps
			
<PROGRAM_F> |=
			<FUNCTION>
			| <INSTRUCTION>

<FUNCTION> |=
			SUB IDENTIFIER <FUNCTION_ARGUMENT> LBRACE <INSTRUCTION_LIST> RBRACE

<FUNCTION_ARGUMENT> |=
			LPAR <ARGUMENT_LIST> RPAR
			| eps

<ARGUMENT_LIST> |=
			VAR <ARGUMENT_LIST_V>
			| eps

<ARGUMENT_LIST_V> |=
			COMMA VAR <ARGUMENT_LIST_V>
			| eps

<INSTRUCTION_LIST> |=
			<INSTRUCTION> <INSTRUCTION_LIST>
			| eps

<FUNCTION_CALL> |=
			CALL_MARK IDENTIFIER LPAR <ARGUMENT_CALL_LIST> RPAR

<ARGUMENT_CALL_LIST> |=
			<EXPRESSION> <ARGUMENT_CALL_LIST_V>
			| eps

<ARGUMENT_CALL_LIST_V> |=
			COMMA <EXPRESSION> <ARGUMENT_CALL_LIST_V>
			| eps

<INSTRUCTION> |=
			<EXPRESSION> <INSTRUCTION_F> SEMICOLON
			| RETURN <EXPRESSION> <INSTRUCTION_F> SEMICOLON
			| LBRACE <INSTRUCTION_LIST> RBRACE
			| <CONDITION> <EXPRESSION> LBRACE <INSTRUCTION_LIST> RBRACE <CONDITION_END>
			
<INSTRUCTION_F> |=
			<CONDITION> <EXPRESSION>
			| eps
			
<CONDITION> |=
			IF	
			| UNLESS
			
<CONDITION_END> |=
			ELSIF <EXPRESSION> LBRACE <INSTRUCTION_LIST> RBRACE <CONDITION_END>
			| ELSE LBRACE <INSTRUCTION_LIST> RBRACE
			| eps

<SIMPLE_EXPRESSION> |=
			<FUNCTION_CALL>
			| INTEGER
			| FLOAT
			| STRING
			| VAR

<EXPRESSION> |=
			<EXPRESSION_TWO> <EXPRESSION_V>

<EXPRESSION_V> |=
			ASSIGN_MARK <EXPRESSION_TWO> <EXPRESSION_V>
			| eps
			
<EXPRESSION_TWO> |=
			<EXPRESSION_FOUR> <EXPRESSION_TWO_V>

<EXPRESSION_TWO_V> |=
			LAZY_OR <EXPRESSION_FOUR> <EXPRESSION_TWO_V>
			| eps

<EXPRESSION_FOUR> |=
			<EXPRESSION_FIVE> <EXPRESSION_FOUR_V>

<EXPRESSION_FOUR_V> |=
			LAZY_AND <EXPRESSION_FIVE> <EXPRESSION_FOUR_V>
			| eps
			
<EXPRESSION_FIVE> |=
			<EXPRESSION_SIX> <EXPRESSION_FIVE_V>
			
<EXPRESSION_FIVE_V> |=
			<EXPRESSION_FIVE_F> <EXPRESSION_SIX>
			| eps
			
<EXPRESSION_FIVE_F> |=
			DIFFERENT
			| EQ
			| EQUALS
			| NE

<EXPRESSION_SIX> |=
			<EXPRESSION_SEVEN> <EXPRESSION_SIX_V>
			
<EXPRESSION_SIX_V> |=
			<EXPRESSION_SIX_F> <EXPRESSION_SEVEN>
			| eps
			
<EXPRESSION_SIX_F> |=
			GE
			| GREATER
			| GREATER_EQUALS
			| GT
			| LE
			| LOWER
			| LOWER_EQUALS
			| LT

<EXPRESSION_SEVEN> |=
			<EXPRESSION_EIGHT> <EXPRESSION_SEVEN_V>
			
<EXPRESSION_SEVEN_V> |=
			<EXPRESSION_SEVEN_F> <EXPRESSION_EIGHT> <EXPRESSION_SEVEN_V>
			| eps
			
<EXPRESSION_SEVEN_F> |=
			PLUS
			| MINUS
			| CONCAT_MARK

<EXPRESSION_EIGHT> |=
			<EXPRESSION_NINE> <EXPRESSION_EIGHT_V>
			
<EXPRESSION_EIGHT_V> |=
			<EXPRESSION_EIGHT_F> <EXPRESSION_NINE> <EXPRESSION_EIGHT_V>
			| eps
			
<EXPRESSION_EIGHT_F> |=
			TIMES
			| DIVIDE

<EXPRESSION_NINE> |=
			<EXPRESSION_NINE_V> <EXPRESSION_TEN>
			
<EXPRESSION_NINE_V> |=
			<EXPRESSION_NINE_F> <EXPRESSION_NINE_V>
			| eps

<EXPRESSION_NINE_F> |=
			NOT
			| PLUS
			| MINUS

<EXPRESSION_TEN> |=
			LPAR <EXPRESSION> RPAR
			| <SIMPLE_EXPRESSION>
