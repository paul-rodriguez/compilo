<PROGRAM> |=
			<PROGRAM_F> <PROGRAM_V>

<PROGRAM_V> |=
			<PROGRAM_F> <PROGRAM_V>
			| eps
			
<PROGRAM_F> |=
			<FUNCTION>
			| <INSTRUCTION>

<FUNCTION> |=
			SUB IDENTIFIER <FUNCTION_ARGUMENT> LBRACE <INSTRUCTION_LIST> RBRACE

<FUNCTION_ARGUMENT> |=
			LPAR <ARGUMENT_LIST> RPAR
			| eps

<ARGUMENT_LIST> |=
			VAR <ARGUMENT_LIST_V>
			| eps

<ARGUMENT_LIST_V> |=
			COMMA VAR <ARGUMENT_LIST_V>
			| eps

<INSTRUCTION_LIST> |=
			<INSTRUCTION> <INSTRUCTION_LIST>
			| eps

<FUNCTION_CALL> |=
			CALL_MARK IDENTIFIER LPAR <ARGUMENT_CALL_LIST> RPAR

<ARGUMENT_CALL_LIST> |=
			<EXPRESSION> <ARGUMENT_CALL_LIST_V>
			| eps

<ARGUMENT_CALL_LIST_V> |=
			COMMA <EXPRESSION> <ARGUMENT_CALL_LIST_V>
			| eps

<INSTRUCTION> |=
			<EXPRESSION> <INSTRUCTION_F> SEMICOLON
			| RETURN <EXPRESSION> <INSTRUCTION_F> SEMICOLON
			| LBRACE <INSTRUCTION_LIST> RBRACE
			| <CONDITION> <EXPRESSION> LBRACE <INSTRUCTION_LIST> RBRACE <CONDITION_END>
			
<INSTRUCTION_F> |=
			<CONDITION> <EXPRESSION>
			| eps
			
<CONDITION> |=
			IF
			| UNLESS
			
<CONDITION_END> |=
			ELSIF <EXPRESSION> LBRACE <INSTRUCTION_LIST> RBRACE <CONDITION_END>
			| ELSE LBRACE <INSTRUCTION_LIST> RBRACE
			| eps

<SIMPLE_EXPRESSION> |=
			<FUNCTION_CALL>
			| INTEGER
			| FLOAT
			| STRING
			| VAR

<EXPRESSION> |=
			NOTLETTERS <EXPRESSION>
			| <EXPRESSION_TWO>

<EXPRESSION_TWO> |=
			<EXPRESSION_THREE> <EXPRESSION_TWO_V>

<EXPRESSION_TWO_V> |=
			ASSIGN_MARK <EXPRESSION_THREE> <EXPRESSION_TWO_V>
			| eps
			
<EXPRESSION_THREE> |=
			<EXPRESSION_FOUR> <EXPRESSION_THREE_V>

<EXPRESSION_THREE_V> |=
			LAZY_OR <NOT_LIST_THREE>
			| eps

<NOT_LIST_THREE> |=
			NOTLETTERS <NOT_LIST_THREE>
			| <EXPRESSION_FOUR> <EXPRESSION_THREE_V>

<EXPRESSION_FOUR> |=
			<EXPRESSION_FIVE> <EXPRESSION_FOUR_V>

<EXPRESSION_FOUR_V> |=
			LAZY_AND <NOT_LIST_FOUR>
			| eps

<NOT_LIST_FOUR> |=
			NOTLETTERS <NOT_LIST_FOUR>
			| <EXPRESSION_FIVE> <EXPRESSION_FOUR_V>
			
<EXPRESSION_FIVE> |=
			<EXPRESSION_SIX> <EXPRESSION_FIVE_V>
			
<EXPRESSION_FIVE_V> |=
			<EXPRESSION_FIVE_F> <NOT_LIST_FIVE>
			| eps
			
<EXPRESSION_FIVE_F> |=
			DIFFERENT
			| EQ
			| EQUALS
			| NE

<NOT_LIST_FIVE> |=
			NOTLETTERS <NOT_LIST_FIVE>
			| <EXPRESSION_SIX>

<EXPRESSION_SIX> |=
			<EXPRESSION_SEVEN> <EXPRESSION_SIX_V>
			
<EXPRESSION_SIX_V> |=
			<EXPRESSION_SIX_F> <NOT_LIST_SIX>
			| eps
			
<EXPRESSION_SIX_F> |=
			GE
			| GREATER
			| GREATER_EQUALS
			| GT
			| LE
			| LOWER
			| LOWER_EQUALS
			| LT

<NOT_LIST_SIX> |=
			NOTLETTERS <NOT_LIST_SIX>
			| <EXPRESSION_SEVEN>

<EXPRESSION_SEVEN> |=
			<EXPRESSION_EIGHT> <EXPRESSION_SEVEN_V>
			
<EXPRESSION_SEVEN_V> |=
			<EXPRESSION_SEVEN_F> <NOT_LIST_SEVEN>
			| eps
			
<EXPRESSION_SEVEN_F> |=
			PLUS
			| MINUS
			| CONCAT_MARK

<NOT_LIST_SEVEN> |=
			NOTLETTERS <NOT_LIST_SEVEN>
			| <EXPRESSION_EIGHT> <EXPRESSION_SEVEN_V>

<EXPRESSION_EIGHT> |=
			<EXPRESSION_NINE> <EXPRESSION_EIGHT_V>
			
<EXPRESSION_EIGHT_V> |=
			<EXPRESSION_EIGHT_F> <NOT_LIST_EIGHT>
			| eps
			
<EXPRESSION_EIGHT_F> |=
			TIMES
			| DIVIDE

<NOT_LIST_EIGHT> |=
			NOTLETTERS <NOT_LIST_EIGHT>
			| <EXPRESSION_NINE> <EXPRESSION_EIGHT_V>

<EXPRESSION_NINE> |=
			<EXPRESSION_NINE_V> <NOT_LIST_NINE>
			
<EXPRESSION_NINE_V> |=
			<EXPRESSION_NINE_F> <EXPRESSION_NINE_V>
			| eps

<EXPRESSION_NINE_F> |=
			NOT
			| PLUS
			| MINUS

<NOT_LIST_NINE> |=
			NOTLETTERS <NOT_LIST_NINE>
			| <EXPRESSION_TEN>

<EXPRESSION_TEN> |=
			LPAR <EXPRESSION> RPAR
			| <SIMPLE_EXPRESSION>
